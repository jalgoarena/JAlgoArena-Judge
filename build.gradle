buildscript {
    ext.kotlin_version = '1.0.6'
    ext.spring_version = '1.5.1.RELEASE'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "jacoco"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Judge Service"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

jar {
    baseName = 'jalgoarena-judge'
}

dependencies {
    compile 'com.google.guava:guava:18.0',
            'com.fasterxml.jackson.core:jackson-databind',
            'com.fasterxml.jackson.core:jackson-core',
            'com.fasterxml.jackson.core:jackson-annotations',
            "com.fasterxml.jackson.module:jackson-module-kotlin",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-undertow",
            "org.springframework.boot:spring-boot-starter-actuator",
            'org.springframework.cloud:spring-cloud-starter-eureka',
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-compiler:$kotlin_version"

    runtime 'ch.qos.logback:logback-classic:1.1.7',
            'ch.qos.logback:logback-core:1.1.7',
            "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"

    testCompile 'junit:junit:4.12',
            'pl.pragmatists:JUnitParams:1.0.5',
            "org.springframework.boot:spring-boot-starter-test",
            'com.squareup.okhttp3:okhttp:3.4.2'
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

bootRun {
    systemProperty 'sun.zip.disableMemoryMapping', 'true'
}

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR3"
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    from('lib/') {
        include('*.jar')
        into('lib')
    }
    from('build/classes/main/com/jalgoarena/type') {
        into('build/classes/main/com/jalgoarena/type')
        include('*.class')
    }
    from('build/resources/main') {
        into('build/resources/main')
        include('**/*.yml', '**/*.xml')
    }
    archiveName "JAlgoArena-Judge-${version}.zip"
    destinationDir(file('build/libs'))
}
