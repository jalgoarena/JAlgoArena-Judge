buildscript {
    ext.kotlin_version = '1.2.31'
    ext.spring_boot_version = '1.5.10.RELEASE'
    ext.jruby_version = '9.1.14.0'
    ext.fasterxml_version = '2.9.5'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "jacoco"
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Judge Service"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

jar {
    baseName = 'jalgoarena-judge'
}

dependencies {
    compile 'com.google.guava:guava:18.0',
            "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "org.springframework.kafka:spring-kafka",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-undertow",
            "org.springframework.boot:spring-boot-starter-actuator",
            'org.springframework.cloud:spring-cloud-starter-eureka',
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-compiler:$kotlin_version",
            "org.jruby:jruby-complete:$jruby_version"

    runtime "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"

    testCompile 'junit:junit:4.12',
            'pl.pragmatists:JUnitParams:1.0.5',
            "org.springframework.kafka:spring-kafka-test",
            "org.springframework.boot:spring-boot-starter-test",
            'com.squareup.okhttp3:okhttp:3.4.2',
            'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

bootRun {
    systemProperty 'sun.zip.disableMemoryMapping', 'true'
}

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3"
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    from('lib/') {
        include('*.jar')
        into('lib')
    }
    from('build/classes/main/com/jalgoarena/type') {
        into('build/classes/main/com/jalgoarena/type')
        include('*.class')
    }
    from('build/resources/main') {
        into('build/resources/main')
        include('**/*.yml', '**/*.xml')
    }
    archiveName "JAlgoArena-Judge-${version}.zip"
    destinationDir(file('build/libs'))
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
